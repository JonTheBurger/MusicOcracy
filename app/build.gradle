apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "com.musicocracy.fpgk.musicocracy"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'
    }
    aaptOptions {
        cruncherEnabled = false // workaround to make gradle recognize png files
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven { url "https://jitpack.io" }
}

protobuf {
    protoc {
        // Download protobuf compiler
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        // all() returns the collection of all protoc tasks
        all().each { task ->
            task.plugins {
                javalite {
                    outputSubDir = 'main'
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Dagger 2 - Dependency Injection Framework
    compile 'com.google.dagger:dagger:2.7'
    apt "com.google.dagger:dagger-compiler:2.7"

    // Butter Knife - View Injection Framework
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    // RX Java - Functional Reactive Java
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // RX Netty - RX Networking
    compile 'io.reactivex:rxnetty:0.4.20'

    // OrmLite - Database Object Relational Mapping
    compile 'com.j256.ormlite:ormlite-core:5.0'
    compile 'com.j256.ormlite:ormlite-android:5.0'

    // Google Protocol Buffers - Binary Serializable Class Generation
    compile 'com.google.protobuf:protobuf-lite:3.0.0'

    // This library handles authentication and authorization
    compile 'com.spotify.sdk:spotify-auth:beta22-noconnect-2.20b@aar'

    // This library handles music playback
    compile 'com.spotify.sdk:spotify-player:24-noconnect-2.20b@aar'

    // Spotify web API wrapper
    compile 'com.github.kaaes:spotify-web-api-android:0.4.1'
    compile 'org.slf4j:slf4j-simple:1.7.21'

    // Unit testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile "com.crittercism.dexmaker:dexmaker:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-dx:1.4"
    androidTestCompile "com.crittercism.dexmaker:dexmaker-mockito:1.4"
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
}

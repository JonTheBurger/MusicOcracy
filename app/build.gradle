apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "com.musicocracy.fpgk.musicocracy"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/INDEX.LIST'
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    maven { url "https://jitpack.io" }
}

protobuf {
    protoc {
        // Download protobuf compiler
        artifact = 'com.google.protobuf:protoc:3.0.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        // all() returns the collection of all protoc tasks
        all().each { task ->
            task.plugins {
                javalite {
                    outputSubDir = 'main'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.+"
    compile 'com.android.support:appcompat-v7:24.2.0'

    // Dagger 2 and Compiler
    compile 'com.google.dagger:dagger:2.6.1'
    apt "com.google.dagger:dagger-compiler:2.6.1"

    // RX Java
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    // RX Netty
    compile 'io.reactivex:rxnetty:0.4.19'

    // OrmLite
    compile 'com.j256.ormlite:ormlite-core:5.0'
    compile 'com.j256.ormlite:ormlite-android:5.0'

    // Google Guava
    compile 'com.google.guava:guava:19.0'

    // Google Protocol Buffers
    compile 'com.google.protobuf:protobuf-lite:3.0.0'
    compile 'com.google.protobuf:protobuf-java-util:3.0.0'

    // This library handles authentication and authorization
    compile 'com.spotify.sdk:spotify-auth:beta22-noconnect-2.20b@aar'

    // This library handles music playback
    compile 'com.spotify.sdk:spotify-player:beta22-noconnect-2.20b@aar'

    // Spotify web API wrapper
    compile 'com.github.kaaes:spotify-web-api-android:0.4.1'
}
